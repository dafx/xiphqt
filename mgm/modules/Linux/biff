# -*-Perl-*-

# the best beginning module example.  Very simple, uses the standard hooks

use Tk;

package MGMmodule::biff;
use vars qw($graph $maildrop $lastmod);

sub module_init{
    my$this=shift;
    my$toplevel=$this->{"toplevel"};
    my$xpath=$this->{"xpath"};

    # modify defaults
    $toplevel->optionAdd("$xpath*scalerefresh",5000,21); # slower
    $toplevel->optionAdd("$xpath*scalereturn",0,21);
    $toplevel->optionAdd("$xpath*label", "mail",21);
    $toplevel->optionAdd("$xpath.scalewidadj", 80,21);   # narrower
    $toplevel->optionAdd("$xpath.scalelenadj", 60,21);   # shorter
    $toplevel->optionAdd("$xpath.bar.0.litbackground", '#cccccc',21);
    
    my($minx,$miny)=&MGM::Graph::calcxysize($this,1024*1024*512,' messages',1);

    $toplevel->optionAdd("$xpath.minx",        $minx,21);      
    $toplevel->optionAdd("$xpath.miny",        $miny,21);      

    $maildrop=$this->{"widget"}->optionGet("maildrop","");

    if(!defined($maildrop)){
	my$name=getpwuid($<);
	if(!defined($name)){
	    print "Couldn't get username when looking for maildrop.\n";
	    $toplevel->optionAdd("$xpath.active", 0,21);
	    1;
	}else{
	    if(-r "/var/spool/mail/"){
		$maildrop="/var/spool/mail/$name";
	    }else{
		if(-r "/var/mail/"){
		    $maildrop="/var/mail/$name";
		}else{
		    print "Couldn't find maildrop directory\n";
		    $toplevel->optionAdd("$xpath.active", 0,21);
		}
	    }
	}
    }
    1;
}

sub module_construct{
    my$this=shift;
    
    $graph=MGM::Graph->new($this,num=>1,prompt=>' messages',minscale=>4);
    $this->module_update;
    $graph->{"widget"};        # must return the widget
}

$lastmod=0;
sub module_update{ 
    my$count=0;
    my$modtime=(stat $maildrop)[9];
    if(defined($modtime)){
	if($modtime!=$lastmod){
	    if (open(PROC,"<$maildrop")){
		while(<PROC>){
		    $count++ if(m/^From /);
		}
		close PROC;
	    }
	    $lastmod=$modtime;
	    $graph->set($count);
	}
    }else{
	$graph->set(0);
    }
}

bless {};

